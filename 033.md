Inspex

medium

# Rewards are sent directly to `owner()` instead of the vault, allowing users to claim reward without paying reward fees

## Summary
The vault owner can claim the reward directly without paying token fees because of the `auraRewardPool().getReward(owner(), true)` function at line 265. 

On this line, the `getReward()` function is called with `owner()` used as a parameter to claim the reward, which is the address of the vault's owner, not the address of the vault itself.

As a result, the reward will be sent directly to `owner()` without any reward fees being paid to the `TRSRY()` address.

## Vulnerability Detail
The `claimRewards()` function is used to claim outstanding rewards from the `Aura` to the vault at line 265.

https://github.com/sherlock-audit/2023-03-olympus/blob/main/sherlock-olympus/src/policies/BoostedLiquidity/BLVaultLido.sol#L262-L269
```solidity
    /// @inheritdoc IBLVaultLido
    function claimRewards() external override onlyWhileActive onlyOwner nonReentrant {
        // Claim rewards from Aura
        auraRewardPool().getReward(owner(), true);

        // Send rewards to owner
        _sendRewards();
    }
```

On line 268, the `_sendRewards()` function is used to send any rewards in the vault to the owner, while also paying a small fee to the `TRSRY()` address if applicable.

https://github.com/sherlock-audit/2023-03-olympus/blob/main/sherlock-olympus/src/policies/BoostedLiquidity/BLVaultLido.sol#L401-L447
```solidity
    function _sendRewards() internal {
        // Send Bal rewards to owner
        {
            uint256 balRewards = bal().balanceOf(address(this));
            uint256 balFee = (balRewards * fee()) / 10_000;
            if (balRewards - balFee > 0) {
                bal().safeTransfer(owner(), balRewards - balFee);
                emit RewardsClaimed(address(bal()), balRewards - balFee);
            }
            if (balFee > 0) bal().safeTransfer(TRSRY(), balFee);
        }

        // Send Aura rewards to owner
        {
            uint256 auraRewards = aura().balanceOf(address(this));
            uint256 auraFee = (auraRewards * fee()) / 10_000;
            if (auraRewards - auraFee > 0) {
                aura().safeTransfer(owner(), auraRewards - auraFee);
                emit RewardsClaimed(address(aura()), auraRewards - auraFee);
            }
            if (auraFee > 0) aura().safeTransfer(TRSRY(), auraFee);
        }

        // Send extra rewards to owner
        {
            uint256 numExtraRewards = auraRewardPool().extraRewardsLength();
            for (uint256 i; i < numExtraRewards; ) {
                IAuraRewardPool extraRewardPool = IAuraRewardPool(auraRewardPool().extraRewards(i));
                ERC20 extraRewardToken = ERC20(extraRewardPool.rewardToken());

                uint256 extraRewardAmount = extraRewardToken.balanceOf(address(this));
                uint256 extraRewardFee = (extraRewardAmount * fee()) / 10_000;
                if (extraRewardAmount - extraRewardFee > 0) {
                    extraRewardToken.safeTransfer(owner(), extraRewardAmount - extraRewardFee);
                    emit RewardsClaimed(
                        address(extraRewardToken),
                        extraRewardAmount - extraRewardFee
                    );
                }
                if (extraRewardFee > 0) extraRewardToken.safeTransfer(TRSRY(), extraRewardFee);

                unchecked {
                    ++i;
                }
            }
        }
    }
```

Because the `auraRewardPool().getReward(owner(), true)` function on line 265 uses `owner()` as a parameter, any reward claimed will be sent directly to the `owner()` rather than the owner's vault. Therefore, if there are no rewards in the vault, the `_sendRewards()` function on line 268 will not transfer any fee to the `TRSRY()` address.

## Impact
The platform will lose rewards fees because users can claim rewards without paying reward fees.

## Code Snippet
https://github.com/sherlock-audit/2023-03-olympus/blob/main/sherlock-olympus/src/policies/BoostedLiquidity/BLVaultLido.sol#L265

## Tool used

Manual Review

## Recommendation
We suggest changing the parameter in the `getReward()` function on line 265 from `owner()` to `address(this)`. For example:

```solidity
    /// @inheritdoc IBLVaultLido
    function claimRewards() external override onlyWhileActive onlyOwner nonReentrant {
        // Claim rewards from Aura
        auraRewardPool().getReward(address(this), true);

        // Send rewards to owner
        _sendRewards();
    }
```
